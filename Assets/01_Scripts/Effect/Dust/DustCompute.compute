// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

RWStructuredBuffer<float3> _Instances;

// 0 ~ 1사이의 랜덤값이다 
StructuredBuffer<float> _ShowRectRand;
float4 _Rect; // xy -> pos , zw -> size

// 남은 시간을 받아와서
// 남은시간이 적을수록 점점 사라질 확률을 높여준다.
float _TotalTime;
float _LeftTime;

float2 _BoxierValue; // from 0 to 1

float2 _Dir;

float nrand(float2 uv)
{
    return frac(sin(dot(uv, float2(12.9898, 78.233))) * 43758.5453);
}

uint2 to2D(float id)
{
    return uint2(id % _Rect.z, id / _Rect.z);
}

[numthreads(256,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    float2 posOS = float2(to2D(id.x));
    float extentX = _Rect.z * 0.5;
    float passedTime = _TotalTime - _LeftTime;
    
    float clip = _ShowRectRand[id.x] - smoothstep(0, _TotalTime, passedTime)
        - smoothstep(0, _Rect.w , posOS.y * (1 - _BoxierValue.y))
        - smoothstep(0, extentX, abs(posOS.x - extentX) * (1 - _BoxierValue.x));
    
    float2 dirOffset = round(_Dir * passedTime);
    
    float2 posWS = posOS + _Rect.xy + 0.5f + dirOffset;
    
    _Instances[id.x] = float3(posWS, clip); // 그리드 맞추기위해 +0.5f 
}
